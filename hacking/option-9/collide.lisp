(in-package :option-9)

(defmethod collide (fist face) nil)
(defmethod collide ((fist collidable) (face collidable))
  (when (and (alivep fist) (alivep face)
	     (vulnerable fist) (vulnerable face))
    (when (< (distance fist face) (max (radius fist) (radius face)))
      (perform-collide fist face))))
(defmethod collide :before ((fist collidable) (face ship))
  (when (and (alivep fist) (alivep face)
	     (vulnerable fist) (vulnerable face))
    (let ((payload (payload (turret face :passive-weapon-port))))
      (when payload
	(collide payload fist)))))
(defmethod collide ((f tesla-field) (face collidable))
  (when (and (alivep face) (vulnerable face))
    (when (contacts f face)
      (perform-collide f face))))
(defmethod collide ((f tesla-field) (face field-mine))
  (when (alivep face)
    (when (contacts f face)
      (perform-collide f face))))
